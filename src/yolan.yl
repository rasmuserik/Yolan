[set yolan [#]]

[if-else [[exports jsType] == 'undefined]
  [set exports yolan]
  [set yolan exports]]

[if [[[module jsType] == 'undefined] fails] [module set 'exports yolan]]

[def engine undefined]

[; Node]

[if [[[process jsType] == 'undefined] fails] [set engine 'node]]

[; Rhino]

[if [[[java jsType] == 'undefined] fails]
  [set engine 'rhino]
  [if [[require jsType] == 'undefined]
    [def modules [# ['./yolan yolan]]]
    [set require
      [fn [name]
        [if [[modules get name] fails]
          [console log 'loading\ module name]
          [def prevModule module]
          [def prevExports exports]
          [set module [#]]
          [set exports [#]]
          [module set 'exports exports]
          [module set 'require require]
          [[Function call null [readFile call null ['build/ + name '.js]]]
            call]
          [modules set name
            [module 'exports]
            [set module prevModule]
            [set exports prevExports]]]
        [return [modules get name]]]]
    [set console [#]]
    [console set
      'log
      [fn []
        [return
          [print call null
            [[[[Array 'prototype] 'slice] call arguments 0] join '\ ]]]]]
    [set module [#]]
    [module set 'require require]]]

[; Browser]

[if [[[navigator jsType] == 'undefined] fails]
  [def loadedModules [# ['yolan yolan]]]
  [set engine 'web]
  [if [[window 'require] fails]
    [def modules [#]]
    [def require
      [fn [name]
        [if [[loadedModules get name] fails]
          [console log 'initialising name]
          [def module [#]]
          [def exports [#]]
          [module set 'exports exports]
          [module set 'require require]
          [[modules get name] call null require exports module]
          [loadedModules set name [module 'exports]]]
        [return [loadedModules get name]]]]
    [window set
      'define
      [fn [name dep f] [modules set name f] [return undefined]]]
    [console log 'def 'require]
    [window set 'require require]]]

[; yolan defs]

[yolan set 'engine engine]

[; Shims]

[if [[console jsType] == 'undefined]
  [set console [#]]
  [console set 'log [fn [] [return undefined]]]
  [if [[[print jsType] == 'undefined] fails] [console set 'log print]]]

[if [[yolan 'engine] fails] [console log 'Error\ detecting\ engine:]]

[Section: Cross-platform file-io
  [if [engine == 'node]
    [yolan set
      'readTextFile
      [fn [fname callback]
        [return [[module require 'fs] readFile fname 'utf8 callback]]]]]
  [if [engine == 'web]
    [yolan set
      'readTextFile
      [fn [fname callback]
        [def req [new XMLHttpRequest]]
        [req set
          'onreadystatechange
          [fn [event]
            [if [[req 'readyState] == 4]
              [if [[[req 'status] == 200] fails]
                [return
                  [callback call null
                    [# ['err 'Request\ error:\ status\ not\ 200\ OK]
                      ['req req]]]]]
              [callback call null null [req 'responseText]]]
            [return undefined]]]
        [req open 'GET ['/readTextFile/ + fname] true]
        [return [req send null]]]]]
  [if [engine == 'node]
    [yolan set
      'writeTextFile
      [fn [fname content callback]
        [return
          [[module require 'fs] writeFile fname content 'utf8 callback]]]]]
  [if [engine == 'web]
    [yolan set
      'writeTextFile
      [fn [fname data callback]
        [def req [new XMLHttpRequest]]
        [req set
          'onreadystatechange
          [fn [event]
            [if [callback fails] return]
            [if [[req 'readyState] == 4]
              [if [[[req 'status] == 200] fails]
                [return
                  [callback call null
                    [# ['err 'Request\ error:\ status\ not\ 200\ OK]
                      ['req req]]]]]
              [callback call null null]]
            [return undefined]]]
        [req open 'POST ['/writeTextFile/ + fname] true]
        [return [req send data]]]]]]

[Section: Utilities
  [yolan set
    'arrayHasMember
    [fn [array member]
      [if-else [[array indexOf member] == -1] [return false] [return true]]]]]
