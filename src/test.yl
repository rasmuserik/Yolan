[def test exports]

[Section: testing library
  [def proto
    [#
      ['fail
        [fn [message] [console log 'Test\ failure: [this 'name] message]]]
      ['assert
        [fn [a message]
          [if [a fails] [console log 'assert a 'fails\ in: message]]]]
      ['assertEqual
        [fn [a b]
          [console log 'AssertEqual a b '( [this 'name] ')]
          [try-catch e
            [if [[[JSON stringify a] == [JSON stringify b]] fails]
              [console log 'Error\ not\ equal a b 'in [this 'name]]]
            [console log
              'Error\ could\ not\ compare
              a b
              'in
              [test 'name]
              'error:
              e]]]]
      ['done [fn [] [console log 'Test\ done [this 'name]]]]]]
  [test set
    'case
    [fn [name]
      [console log 'Starting\ testcase: name]
      [def result [Object create proto]]
      [result set 'name name]
      [return result]]]]

[def yolan [module require './yolan]]

[test set
  'run
  [fn [args]
    [Section: yolan file i/o
      [def testcase [test case 'yolanIO]]
      [yolan writeTextFile
        'test-tmp/yolanIOtest
        'testvalue
        [fn [err]
          [if err [testcase fail 'writeError]]
          [yolan readTextFile
            'test-tmp/yolanIOtest
            [fn [err data]
              [if err [testcase fail 'readError]]
              [testcase assertEqual data 'testvalue]
              [testcase done]]]]]]
    [Section: yolan utility
      [def testcase [test case 'yolanUtility]]
      [testcase assert
        [yolan arrayHasMember '[1 2 3 4] 3]
        'arrayHasMember1]
      [testcase assert
        [[yolan arrayHasMember '[1 2 3 4] 7] fails]
        'arrayHasMember2]
      [testcase done]]]]
